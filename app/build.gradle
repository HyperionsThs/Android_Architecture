apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-platform-android'
apply from: '../autodimension.gradle'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        applicationId "com.example.framgia.android_architecture"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    dexOptions {
        javaMaxHeapSize "4g"
    }
    signingConfigs {
        def signFile = file("../local.properties")
        def signingProps = new Properties()
        debug {
            storeFile file("../keystores/android.jks")
            storePassword "Android123456"
            keyAlias "android"
            keyPassword "androidpw"
        }
        release {
            signingProps.load(new FileInputStream(signFile))
            storeFile file("../keystores/android.jks")
            storePassword signingProps['storePassword']
            keyAlias signingProps['keyAlias']
            keyPassword signingProps['keyPassword']
        }
    }
    buildTypes {
        debug {
            signingConfigs.debug
        }
        release {
            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
        mock {
            versionNameSuffix = "mock"
            applicationIdSuffix = ".mock"
            manifestPlaceholders = [applicationName: "MOCK"]
            signingConfig signingConfigs.debug
        }
        dev {
            versionNameSuffix = "dev"
            applicationIdSuffix = ".dev"
            manifestPlaceholders = [applicationName: "DEV"]
            signingConfig signingConfigs.debug
        }
        prd {
            manifestPlaceholders = [applicationName: "@string/app_name"]
            signingConfig signingConfigs.release
        }
    }
    flavorDimensions "default"
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support libraries
    implementation "androidx.appcompat:appcompat:1.0.2"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.google.android.material:material:1.1.0-alpha06"
    implementation "androidx.constraintlayout:constraintlayout:1.1.3"
    implementation "androidx.constraintlayout:constraintlayout-solver:1.1.3"

    def daggerVersion = "2.16"
    //Dagger
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    // Architecture components
    implementation "androidx.lifecycle:lifecycle-runtime:2.2.0-alpha01"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0-alpha01"
    kapt "androidx.lifecycle:lifecycle-compiler:2.2.0-alpha01"

    // RxJava
    implementation "io.reactivex.rxjava2:rxandroid:2.1.0"
    implementation "io.reactivex.rxjava2:rxjava:2.2.4"

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.5.0"
    implementation "com.squareup.retrofit2:converter-gson:2.5.0"
    implementation "com.squareup.retrofit2:adapter-rxjava2:2.5.0"

    // OkHttp
    implementation "com.squareup.okhttp3:logging-interceptor:3.12.0"

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.3.31"

    // Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

}

//TODO add when use google services
//apply plugin: 'com.google.gms.google-services'

